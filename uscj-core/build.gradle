import java.util.jar.JarEntry
import java.util.jar.JarFile
import java.util.jar.JarOutputStream

buildscript {
    dependencies {
        classpath files('libs/usc-gradle-witness.jar')
    }
}

plugins {
    id 'application'
}

apply plugin: 'witness'
apply plugin: 'maven'


test {

    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }

    jvmArgs '-Xss32m', '-Xmx3G'

    testLogging {
        events "failed"
        exceptionFormat "short"
    }

   /* jacoco {
        append = false
        destinationFile = file("build/reports/jacoco/jacoco.exec")
    }*/

}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

repositories {
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://deps.rsklabs.io"
    }

}

sourceCompatibility = 1.8

mainClassName = 'co.usc.Start'
applicationDefaultJvmArgs = ["-server", "-Xss32m", "-Xms3G", "-XX:+UseCompressedOops", "-XX:-OmitStackTraceInFastThrow"]


tasks.withType(JavaCompile){
    options.warnings = false
    options.encoding = 'UTF-8'
    options.compilerArgs << '-XDignore.symbol.file'
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

ext {
    slf4jVersion = '1.7.25'
    leveldbVersion = '0.9'
    scastleVersion = '1.52.0.0'
    springVersion = '4.2.9.RELEASE'
    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
    powermockitoVersion = '1.6.4'
    rskLllVersion = '0.0.2'
    logbackVersion = '1.2.2'
}

dependencies {
    compile "io.netty:netty-all:4.0.30.Final"
    compile "com.madgag.spongycastle:core:${scastleVersion}" // for SHA3 and SECP256K1
    compile "com.madgag.spongycastle:prov:${scastleVersion}" // for SHA3 and SECP256K1
    compile "org.iq80.leveldb:leveldb:${leveldbVersion}"
    compile "org.fusesource.leveldbjni:leveldbjni:1.8"
    compile "org.ethereum:leveldbjni-all:1.18.3"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:2.8.7"
    compile "org.apache.commons:commons-collections4:4.1"
    compile "org.apache.commons:commons-lang3:3.5"
    compile ("org.springframework:spring-context:${springVersion}") {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile "com.typesafe:config:1.3.1"
    compile "org.mapdb:mapdb:2.0-beta13"
    compile 'com.github.UlordChain:ulordj-thin:768c50be50'

    compile 'org.apache.httpcomponents:httpclient:4.5.5'
    compile 'org.json:json:20180130'

    compile 'com.github.briandilley.jsonrpc4j:jsonrpc4j:1.5.1'

    runtime "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    runtime "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
    runtime "ch.qos.logback:logback-classic:${logbackVersion}"
    runtime "ch.qos.logback:logback-core:${logbackVersion}"

    testCompile "com.googlecode.json-simple:json-simple:1.1.1"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.powermock:powermock-core:${powermockitoVersion}"
    testCompile "org.powermock:powermock-module-junit4:${powermockitoVersion}"
    testCompile "org.powermock:powermock-api-mockito:${powermockitoVersion}"
    testCompile "co.rsk:lll-compiler:${rskLllVersion}"
    testCompile "org.springframework:spring-tx:${springVersion}"
    testCompile "org.springframework:spring-orm:${springVersion}"
    testCompile "com.h2database:h2:1.4.193"
    testCompile "org.awaitility:awaitility:3.0.0"
    testCompile 'commons-io:commons-io:2.5'
    testCompile 'commons-codec:commons-codec:1.10'
}

javadoc {
    options.author = true
    options.header = "UscJ"
    options.addStringOption('-quiet')
    options.encoding = "UTF-8"
}

def generatedResources = "$buildDir/generated-resources"
sourceSets {
    generated {
        output.dir(generatedResources, builtBy: 'generateResources')
    }
}

task generateResources << {
    mkdir generatedResources
    def generated = new File(generatedResources, 'build-info.properties')
    def commitHash = gitCommitHash()
    def currentBranch = gitCurrentBranch()
    generated.text = """
    build.hash=$commitHash
    build.branch=$currentBranch
    """
}

tasks.withType(Jar) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    dirMode = 0775
    fileMode = 0664
    doLast {
        stripJar(outputs.files.singleFile)
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task jar(type:Jar, overwrite: true) {
    def commitHash = gitCommitHash()
    def currentBranch = gitCurrentBranch()
    manifest {
        attributes  'Git-Commit': commitHash,
                'Git-Branch': currentBranch
    }
    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from sourceSets.generated.output.dirs
}


task generatePom(dependsOn: jar) {
    def jarName = jar.outputs.files.singleFile.name
    def pomFile = "$libsDir/${jarName.take(jarName.lastIndexOf('.'))}.pom"
    ext.file = new File(pomFile)
    doLast {
        pom {
            project {
                groupId project.group
                artifactId project.name
                version project.version

                licenses {
                    license {
                        name 'GNU General Public License (GPL) version 3.0'
                        url 'http://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo(pomFile)
    }
}

task fatJar(type: Jar, dependsOn: jar) {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
    exclude "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA"
    classifier = 'all'
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.withType(Tar) {
    compression = Compression.GZIP
    extension = 'tar.gz'
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
    archives(generatePom.file) {
        type 'text'
        builtBy generatePom
    }
    archives fatJar
    archives distTar
}

def stripJar(File originalFile) {
    def constantTimeForZipEntries = new GregorianCalendar(1980, Calendar.FEBRUARY, 1, 0, 0, 0).timeInMillis
    def reproducibleFile = File.createTempFile("temp", ".tmp")
    reproducibleFile.withOutputStream { fout ->
        def out = new JarOutputStream(fout)
        def jf = new JarFile(originalFile)
        jf.entries().unique {it.name}.sort {it.name}.each {
            def copy = new JarEntry(it.name)
            copy.time = constantTimeForZipEntries
            out.putNextEntry(copy)
            out << jf.getInputStream(it)
        }
        out.finish()
    }
    reproducibleFile.renameTo originalFile
}

def gitCurrentBranch() {
    def process = "git rev-parse --abbrev-ref HEAD".execute()
    return process.text.trim()
}

def gitCommitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.trim()
}

def gitDateHash() {
    def process = "git show -s --format=%ct ${gitCommitHash()}".execute()
    return process.text.toLong()
}
